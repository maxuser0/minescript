// Based on MultiLoader-Template:
// https://github.com/jaredlll08/MultiLoader-Template/blob/1.21/settings.gradle
pluginManagement {
    repositories {
        gradlePluginPortal()
        mavenCentral()
        exclusiveContent {
            forRepository {
                maven {
                    name = 'Fabric'
                    url = uri('https://maven.fabricmc.net')
                }
            }
            filter {
                includeGroup('net.fabricmc')
                includeGroup('fabric-loom')
            }
        }
        exclusiveContent {
            forRepository {
                maven {
                    name = 'Sponge'
                    url = uri('https://repo.spongepowered.org/repository/maven-public')
                }
            }
            filter {
                includeGroupAndSubgroups("org.spongepowered")
            }
        }
        exclusiveContent {
            forRepository {
                maven {
                    name = 'Forge'
                    url = uri('https://maven.minecraftforge.net')
                }
            }
            filter {
                includeGroupAndSubgroups('net.minecraftforge')
            }
        }
    }
}

plugins {
    id 'org.gradle.toolchains.foojay-resolver-convention' version '0.8.0'
}

// This should match the folder name of the project, or else IDEA may complain (see https://youtrack.jetbrains.com/issue/IDEA-317606)
rootProject.name = 'minescript'
include('common')

if (System.getenv("NO_MINESCRIPT_FABRIC_BUILD") != "1") {
  include('fabric')
}

if (System.getenv("NO_MINESCRIPT_NEOFORGE_BUILD") != "1") {
  include('neoforge')
}

if (System.getenv("NO_MINESCRIPT_FORGE_BUILD") != "1") {
  include('forge')
}
